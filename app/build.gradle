apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"
    defaultConfig {
        applicationId "co.otaoto"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "co.otaoto.TestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "api"
    productFlavors {
        production {
            dimension "api"
        }
        mocked {
            dimension "api"
        }
    }
    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDirs += commonTestDir
        }
        androidTest {
            java.srcDirs += commonTestDir
        }
    }
}

ext {
    archVersion = "1.0.0"
    butterknifeVersion = "8.8.1"
    coroutineVersion = "0.19.3"
    daggerVersion = "2.13"
    espressoVersion = "3.0.1"
    retrofitVersion = "2.3.0"
    supportVersion = "27.0.2"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"

    implementation "android.arch.lifecycle:extensions:$archVersion"
    kapt "android.arch.lifecycle:compiler:$archVersion"
    testImplementation "android.arch.core:core-testing:$archVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    mockedImplementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation 'com.squareup.moshi:moshi:1.5.0'

    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    implementation "com.jakewharton.timber:timber:4.6.0"

    implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.8.2'

    testImplementation 'org.mockito:mockito-core:2.12.0'
    testImplementation "android.arch.core:core-testing:$archVersion"
    testImplementation "junit:junit:4.12"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "android.arch.core:core-testing:$archVersion"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
